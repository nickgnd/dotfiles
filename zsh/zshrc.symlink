ZSH_DIR=$HOME/dotfiles/zsh                              # zsh config directory in dotfiles

# globals
export EDITOR='vim'                                     # default editor
export LANG=en_US.UTF-8                                 # set language to English and UTF-8 encoding
export FZF_DEFAULT_OPTS='--color=16'                    # use the terminal colors for fzf
export LSCOLORS="Gxfxcxdxbxegedabagacad"                # define colors for list command
export CLICOLOR=1                                       # enable colors for list command
export ERL_AFLAGS="-kernel shell_history enabled"       # shell history for Erlang / Elixir
export ERL_EPMD_ADDRESS=127.0.0.1                       # prevent epmd from exposing a port
export HOMEBREW_BUNDLE_NO_LOCK=1                        # disable lockfile for brew bundle

# PATH
typeset -U PATH path                                    # make PATH an array with unique entries
export PATH="$HOME/dotfiles/bin:$PATH"                  # dotfiles bin
export PATH="/usr/local/sbin:$PATH"                     # homebrew sbin
export PATH="/usr/local/opt/openssl/bin:$PATH"          # add openssl

# configure completion
zstyle ':completion:*' list-colors ''                   # use LSCOLORS list colors for completion
zstyle ':completion:*' special-dirs true                # complete the . and .. special directories
zstyle ':completion:*:*:*:*:*' menu select              # show completion as menu and navigate with arrow keys
zstyle ':completion:*' squeeze-slashes true             # treat multiple slashes as multiple directories
zstyle ':completion:*' use-cache yes                    # cache completion
zstyle ':completion:*' cache-path $HOME/.zcompcache     # completion cache directory

# history
HISTFILE=$HOME/.zsh_history
HISTSIZE=50000
SAVEHIST=10000

# options
setopt hist_find_no_dups                                # no duplicates in history
setopt hist_ignore_all_dups                             # ignore all duplicates
setopt extended_history                                 # record timestamp of command
setopt hist_expire_dups_first                           # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups                                 # ignore duplicated in commands history list
setopt hist_ignore_space                                # ignore commands that start with space
setopt hist_verify                                      # show command with history expansion to user before running it
setopt inc_append_history                               # all sessions write to same history
setopt share_history                                    # all sessions read same history
setopt nolistbeep                                       # surpress beep for autocompletion
setopt auto_menu                                        # show completion menu on successive tab press
setopt autocd                                           # automatically change directory without prepending `cd`
bindkey -e                                              # emacs keybindings
bindkey "${terminfo[kcbt]}" reverse-menu-complete       # shift-tab for reverse completion menu

# Fix delete-forward on tmux (https://stackoverflow.com/a/41885766)
bindkey "^[[3~"             delete-char                 # enable delete key on any keyboard
bindkey "^[3;5~"            delete-char                 # enable delete key on any keyboard

# tools
if [ -f "$HOME/.asdf/asdf.sh" ]; then                   # asdf
  source $HOME/.asdf/asdf.sh

  # append completions to fpath
  fpath=(${ASDF_DIR}/completions $fpath)
fi
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh                  # fzf
if which pyenv > /dev/null; then                        # pyenv
  eval "$(pyenv init - zsh --no-rehash)";
fi

# functions
br() {
  local branches branch
  branches=$(git branch --all | grep -v HEAD) &&
  branch=$(echo "$branches" |
           fzf-tmux -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
  git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}

# tat: tmux attach
# https://juliu.is/a-simple-tmux/
function tat {
  name=$(basename `pwd` | sed -e 's/\.//g')

  if tmux ls 2>&1 | grep "$name"; then
    tmux attach -t "$name"
  elif [ -f .envrc ]; then
    direnv exec / tmux new-session -s "$name"
  else
    tmux new-session -s "$name"
  fi
}

custom_prompt_pure_clear_screen() {
  zle -I                   # Enable output to terminal.
  print -n '\e[2J\e[4;0H'  # Clear screen and move cursor to (4, 0).
  zle .redisplay           # Redraw prompt.
}
zle -N clear-screen custom_prompt_pure_clear_screen

# fzf integration with fd
if [ -x "$(command -v fd)" ]; then
  # Respect .gitignore (https://github.com/junegunn/fzf#respecting-gitignore)
  export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

  # Use fd (https://github.com/sharkdp/fd) instead of the default find
  # command for listing path candidates.
  # - The first argument to the function ($1) is the base path to start traversal
  # - See the source code (completion.{bash,zsh}) for the details.
  _fzf_compgen_path() {
    fd --hidden --follow --exclude ".git" . "$1"
  }

  # Use fd to generate the list for directory completion
  _fzf_compgen_dir() {
    fd --type d --hidden --follow --exclude ".git" . "$1"
  }
fi

# aliases
alias be="bundle exec"
alias md='mkdir -p'
alias l='ls -lah'
alias ll='ls -lh'
alias -g ...='../..'
alias vmd="npx 'vmd'"
alias reload!='source ~/.zshrc'

# git aliases (inspired by oh-my-zsh https://github.com/ohmyzsh/ohmyzsh/blob/master/plugins/git/git.plugin.zsh)
alias gst="git status"
alias gb="git branch"
alias gbnm="git branch --no-merged"
alias ga="git add"
alias gaa="git add --all"
alias gc="git commit -v"
alias gc!="git commit -v --amend"
alias gcn!="git commit -v --no-edit --amend"
alias gco="git checkout"
alias gp="git push"
alias gpf="git push --force-with-lease"
alias gd="git diff"
alias gds="git diff --staged"
alias gdt="git diff-tree --no-commit-id --name-only -r"
alias gdw="git diff --word-diff"
alias gfa="git fetch --all --prune --jobs=10"
alias glg="git log --stat"
alias glgp="git log --stat -p"
alias glo="git log --oneline --decorate"
alias glol="git log --graph --pretty='%Cred%h%Creset -%C(auto)%d%Creset %s %Cgreen(%cr) %C(bold blue)<%an>%Creset'"
alias grbi="git rebase -i"
alias gpsup="git pushup" # defined in gitconfig file

# locals
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# prompt
fpath+=$ZSH_DIR/plugins/pure
autoload -U promptinit; promptinit
prompt pure

# init completion
# TODO: speed this up by only loading every 24h
# https://gist.github.com/ctechols/ca1035271ad134841284
autoload -Uz compinit && compinit -d $HOME/.zcompdump   # load completion
autoload bashcompinit && bashcompinit                   # load bash completion

# plugins
source $ZSH_DIR/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
